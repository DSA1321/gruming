&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПериодОбработки = НачалоМесяца(ТекущаяДата());
	МесяцСтрокой = Формат(ПериодОбработки, "ДФ='ММММ гггг'");
	ДатаЗагрузкиНовостей = ТекущаяДата();
	КоличествоНовостей = 10;
	СтрокаПоиска = "Бизнес";
	ЭтотОбъект.ЯзыкПоиска = "ru";
	ПолучитьКурсыВалютПоДаннымЦентробанкаНаСервере();
	ОбновитьНовостиНаСервере();
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеНаСервере()
	ЗаполнитьЧисловыеПоказатели();
	ЗаполнитьГистограммы();
	ЗаполнитьКруговыеДиаграммы();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЧисловыеПоказатели()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПродажиОбороты.Регистратор КАК Регистратор,
	|	ПродажиОбороты.СуммаОборот КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_Продажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_Продажи.СуммаОборот) КАК СуммаОборот
	|ИЗ
	|	ВТ_Продажи КАК ВТ_Продажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_Продажи.СуммаОборот) КАК СуммаОборот,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_Продажи.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТ_КоличествоРегистраторов
	|ИЗ
	|	ВТ_Продажи КАК ВТ_Продажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_КоличествоРегистраторов.Регистратор = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_КоличествоРегистраторов.СуммаОборот / ВТ_КоличествоРегистраторов.Регистратор КАК ЧИСЛО(10, 2))
	|	КОНЕЦ КАК СреднийЧек
	|ИЗ
	|	ВТ_КоличествоРегистраторов КАК ВТ_КоличествоРегистраторов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКлиентов.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ЗаписиКлиентов
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
	|ГДЕ
	|	ЗаказыКлиентов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ЗаказыКлиентов.Регистратор ССЫЛКА Документ.ЗаписьКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЗаписиКлиентов.Регистратор) КАК КоличествоЗаписейКлиентов
	|ИЗ
	|	ВТ_ЗаписиКлиентов КАК ВТ_ЗаписиКлиентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ЗаписиКлиентов.Регистратор) КАК Завершенных
	|ИЗ
	|	ВТ_ЗаписиКлиентов КАК ВТ_ЗаписиКлиентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровИУслуг КАК РеализацияТоваровИУслуг
	|		ПО ВТ_ЗаписиКлиентов.Регистратор = РеализацияТоваровИУслуг.ДокументОснование
	|ГДЕ
	|	РеализацияТоваровИУслуг.Проведен";
	ВсегоЗаписей = 0;
	РезультатПакет = Запрос.ВыполнитьПакет();
	ВыборкаПродажи = РезультатПакет[1].Выбрать();
	Если ВыборкаПродажи.Следующий() Тогда
		ВыручкаЧисло = ВыборкаПродажи.СуммаОборот;
	КонецЕсли;
	ВыборкаСреднийЧек = РезультатПакет[3].Выбрать();
	Если ВыборкаСреднийЧек.Следующий() Тогда
		СреднийЧек = ВыборкаСреднийЧек.СреднийЧек;
	КонецЕсли;
	ВыборкаЗаписиКлиентов = РезультатПакет[5].Выбрать();
	Если ВыборкаЗаписиКлиентов.Следующий() Тогда
		ВсегоЗаписей = ВыборкаЗаписиКлиентов.КоличествоЗаписейКлиентов;
	КонецЕсли;
	ВыборкаЗаписиКлиентовЗавершенные = РезультатПакет[6].Выбрать();
	Если ВыборкаЗаписиКлиентовЗавершенные.Следующий() Тогда
		Завершенных = ВыборкаЗаписиКлиентовЗавершенные.Завершенных;
		Если ВсегоЗаписей > 0 Тогда
			ПроцентЗавершенных = ОКР((100 * Завершенных / ВсегоЗаписей), 2);
			ЗавершенныхПроцентСтрока = СтрШаблон("Это %1 процентов от всех записей", ПроцентЗавершенных); 
		Иначе
			ЗавершенныхПроцентСтрока = "В этом периоде нет записей клиентов!";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГистограммы()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|    НачалоПериода(Продажи.Период, День) КАК Период,
	|    Продажи.СуммаОборот КАК СуммаОборот
	|ИЗ
	|    РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК Продажи
	|
	|УПОРЯДОЧИТЬ ПО
	|    Период
	|ИТОГИ
	|    СУММА(СуммаОборот)
	|ПО
	|    Период ПЕРИОДАМИ(ДЕНЬ, &ДатаНачала, &ДатаОкончания)";	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));	
	ДиаграммаВыручка.Обновление = Ложь;
	ДиаграммаВыручка.Очистить();	
	Серия = ДиаграммаВыручка.Серии.Добавить("Оборот");
	Серия.Цвет = WebЦвета.ЛососьСветлый;	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
	НомерЗаписи = 0;
	ТекущийОстаток = 0;
	ОстатокВчера = 0;
	Пока Выборка.Следующий() Цикл
		Точка = ДиаграммаВыручка.Точки.Добавить(Выборка.Период);
		Точка.Текст = Формат(Выборка.Период, "ДФ=dd.MM.yy");
		Точка.Расшифровка = Выборка.Период;
		Подсказка = "Выручка " + Выборка.СуммаОборот + " на " + Формат(Выборка.Период, "ДФ=dd.MM.yyyy"); 
		ДиаграммаВыручка.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот, Точка.Расшифровка, Подсказка);
	КонецЦикла;
	ДиаграммаВыручка.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКруговыеДиаграммы()
	ЗаполнитьДиаграммуПоРекламнымИсточникам();
	ЗаполнитьДиаграммуПоПитомцам();
	ЗаполнитьДиаграммуПоСотрудникам();
	ЗаполнитьДиаграммуПоУслугам();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоРекламнымИсточникам()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказыКлиентов.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ЗаписиКлиентов
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
	|ГДЕ
	|	ЗаказыКлиентов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ЗаказыКлиентов.Регистратор ССЫЛКА Документ.ЗаписьКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПродажиОбороты.Клиент.Источник.Представление, ""Не указан"") КАК ИсточникИнформации,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПродажиОбороты.Регистратор) КАК КоличествоЗаписей
	|ИЗ
	|	ВТ_ЗаписиКлиентов КАК ВТ_ЗаписиКлиентов,
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ПродажиОбороты.Клиент.Источник.Представление, ""Не указан"")";
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));		
	ДиаграммаПоРекламнымИсточникам.Обновление = Ложь;
	ДиаграммаПоРекламнымИсточникам.Очистить();
	Точка = ДиаграммаПоРекламнымИсточникам.Точки.Добавить("КоличествоЗаписей");
	Точка.Текст = "КоличествоЗаписей";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаПоРекламнымИсточникам.Серии.Добавить(Выборка.ИсточникИнформации);
		ДиаграммаПоРекламнымИсточникам.УстановитьЗначение(Точка, Серия, Выборка.КоличествоЗаписей, Строка(Выборка.КоличествоЗаписей));
	КонецЦикла;
	ДиаграммаПоРекламнымИсточникам.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоПитомцам()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КонтрагентыПитомцы.Ссылка) КАК Количество,
	               |	КонтрагентыПитомцы.ВидПитомца КАК ВидПитомца
	               |ИЗ
	               |	Справочник.Контрагенты.Питомцы КАК КонтрагентыПитомцы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	КонтрагентыПитомцы.ВидПитомца";
	ДиаграммаПоПитомцам.Обновление = Ложь;
	ДиаграммаПоПитомцам.Очистить();
	Точка = ДиаграммаПоПитомцам.Точки.Добавить("Количество");
	Точка.Текст = "Количество";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаПоПитомцам.УстановитьСерию(Выборка.ВидПитомца);
		ДиаграммаПоПитомцам.УстановитьЗначение(Точка, Серия, Выборка.Количество, Строка(Выборка.Количество));
	КонецЦикла;
	ДиаграммаПоПитомцам.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоСотрудникам()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК Сумма,
	|	ПродажиОбороты.Сотрудники.Представление КАК Сотрудник
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Сотрудники.Представление";	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));
	ДиаграммаВыручкаПоСотрудникам.Обновление = Ложь;
	ДиаграммаВыручкаПоСотрудникам.Очистить();
	Точка = ДиаграммаВыручкаПоСотрудникам.Точки.Добавить("Сумма");
	Точка.Текст = "Сумма";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаВыручкаПоСотрудникам.Серии.Добавить(Выборка.Сотрудник);
		ДиаграммаВыручкаПоСотрудникам.УстановитьЗначение(Точка, Серия, Выборка.Сумма, Строка(Выборка.Сумма));
	КонецЦикла;
	ДиаграммаВыручкаПоСотрудникам.Обновление = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуПоУслугам()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПродажиОбороты.Номенклатура КАК Номенклатура,
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_КонкретныеУслуги
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатур.Услуга)) КАК ПродажиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиОбороты.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ПродажиОбороты.СуммаОборот) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТ_ВсеПродажи
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатур.Услуга)) КАК ПродажиОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КонкретныеУслуги.Номенклатура КАК Номенклатура,
	|	ВТ_КонкретныеУслуги.СуммаОборот КАК СуммаПродажНоменклатуры,
	|	ВТ_ВсеПродажи.СуммаОборот КАК ОбщаяСуммаПоУслугам
	|ПОМЕСТИТЬ ВТ_Предытог
	|ИЗ
	|	ВТ_КонкретныеУслуги КАК ВТ_КонкретныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВсеПродажи КАК ВТ_ВсеПродажи
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА 100 * ВТ_Предытог.СуммаПродажНоменклатуры / ВТ_Предытог.ОбщаяСуммаПоУслугам > 10
	|			ТОГДА ВТ_Предытог.Номенклатура.Представление
	|		ИНАЧЕ ""Прочее""
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВТ_Предытог.СуммаПродажНоменклатуры) КАК Сумма
	|ИЗ
	|	ВТ_Предытог КАК ВТ_Предытог
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА 100 * ВТ_Предытог.СуммаПродажНоменклатуры / ВТ_Предытог.ОбщаяСуммаПоУслугам > 10
	|			ТОГДА ВТ_Предытог.Номенклатура.Представление
	|		ИНАЧЕ ""Прочее""
	|	КОНЕЦ";
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОбработки);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодОбработки));
	ДиаграммаВыручкаПоУслугам.Обновление = Ложь;
	ДиаграммаВыручкаПоУслугам.Очистить();
	Точка = ДиаграммаВыручкаПоУслугам.Точки.Добавить("Сумма");
	Точка.Текст = "Сумма";
	Точка.ПриоритетЦвета = Ложь;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Серия = ДиаграммаВыручкаПоУслугам.Серии.Добавить(Выборка.Номенклатура);
		ДиаграммаВыручкаПоУслугам.УстановитьЗначение(Точка, Серия, Выборка.Сумма, Строка(Выборка.Сумма));
	КонецЦикла;
	ДиаграммаВыручкаПоУслугам.Обновление = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Подсказка = "Введите период получения данных";
	ЧастьДаты = ЧастиДаты.Дата;
	Оповещение = Новый ОписаниеОповещения("ПослеВводаПериода",ЭтотОбъект);
	ПоказатьВводДаты(Оповещение, ПериодОбработки, Подсказка, ЧастьДаты);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаПериода(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ПериодОбработки = НачалоМесяца(Результат);
		МесяцСтрокой = Формат(ПериодОбработки, "ДФ='ММММ гггг'");
	КонецЕсли;    
	ЗаполнитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗагрузкиПриИзменении(Элемент)
	ПолучитьКурсыВалютПоДаннымЦентробанкаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьКурсыВалютПоДаннымЦентробанкаНаСервере()
	Сервер = "cbr.ru";
	АдресЗапроса = "/scripts/XML_daily.asp?date_req=" + Формат(ДатаЗагрузкиНовостей, "ДФ=dd/MM/yyyy");
	ВозвращаемоеЗначение = РаботаСHTTP.ПолучитьДанныеИзСервиса(Сервер, АдресЗапроса);
	Если ВозвращаемоеЗначение.УспешныйЗапрос = Ложь Тогда
		Сообщить(ВозвращаемоеЗначение.ТекстОшибки);
		возврат;
	ИначеЕсли ВозвращаемоеЗначение.HTTPОтвет.КодСостояния <> 200 Тогда 
		Сообщить(СтрШаблон("Код вернул код состояния, отличный от 200: %1", ВозвращаемоеЗначение.HTTPОтвет.КодСостояния));
		возврат;
	КонецЕсли;
	СтрокаXML = ВозвращаемоеЗначение.HTTPОтвет.ПолучитьТелоКакСтроку();
	ПрочитатьИзXMLПоследовательно(СтрокаXML);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИзXMLПоследовательно(СтрокаXML)
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Валюта = Неопределено;
	Пока ЧтениеXML.Прочитать() Цикл
		//ищем ID= R01235 (доллар) R01335 (тенге) R01239 (евро)
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			НужнаяВалюта = Ложь;
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				Если  ЧтениеXML.Имя = "ID" И ЧтениеXML.Значение = "R01235" Тогда
					Валюта = "USD";
				ИначеЕсли ЧтениеXML.Имя = "ID" И ЧтениеXML.Значение = "R01239" Тогда
					Валюта = "EURO";
				ИначеЕсли ЧтениеXML.Имя = "ID" И ЧтениеXML.Значение = "R01335" Тогда
					Валюта = "KZT";
				Иначе
					Валюта = Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если Валюта <> Неопределено
			И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
			И ЧтениеXML.Имя = "VunitRate" Тогда
			ЧтениеXML.Прочитать();
			Если Валюта = "USD" Тогда
				КурсДоллараНаСегодня = ЧтениеXML.Значение;
			ИначеЕсли Валюта = "EURO" Тогда
				КурсЕвроНаСегодня = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЕсли;
		Если Валюта <> Неопределено
			И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
			И ЧтениеXML.Имя = "Value" Тогда
			ЧтениеXML.Прочитать();
			Если Валюта = "KZT" Тогда
				КурсТенгеНаСегодня = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ЧтениеXML.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНовостиИКурсыВалют(Команда)
	ОбновитьНовостиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНовостиНаСервере()
	ТопНовости = "";
	Сервер = "newsapi.org"; 
	АдресЗапроса = "/v2/everything";
	ПараметрыЗапроса = СтрШаблон("apiKey=%1&sortBy=popularity&from=%2&q=%3&language=%4", СокрЛП(Константы.API_KEY_newsapi.Получить()),
	Формат(ДатаЗагрузкиНовостей, "ДФ=yyyy-MM-dd"), СокрЛП(СтрокаПоиска), ЯзыкПоиска);	
	АдресЗапроса = АдресЗапроса + "?" + ПараметрыЗапроса;
	ВозвращаемоеЗначение = РаботаСHTTP.ПолучитьДанныеИзСервиса(Сервер, АдресЗапроса);
	Если ВозвращаемоеЗначение.УспешныйЗапрос = Ложь Тогда
		Сообщить(ВозвращаемоеЗначение.ТекстОшибки);
		возврат;
	ИначеЕсли ВозвращаемоеЗначение.HTTPОтвет.КодСостояния <> 200 Тогда
		Сообщить(СтрШаблон("Код вернул код состояния, отличный от 200: %1", ВозвращаемоеЗначение.HTTPОтвет.КодСостояния));
		возврат;
	КонецЕсли;
	СтрокаJSON = ВозвращаемоеЗначение.HTTPОтвет.ПолучитьТелоКакСтроку();
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Новости = ПрочитатьJSON(ЧтениеJSON, Ложь);
	ЧтениеJSON.Закрыть();
	Итератор = 1;
	Если Новости <> Неопределено Тогда
		Для Каждого Статья Из Новости.articles Цикл
			ТопНовости = ТопНовости + Строка(Итератор) + ") " + "Источник: " + Статья.url + Символы.ПС + Статья.title + Символы.ПС;
			ТопНовости = ТопНовости + Статья.description + Символы.ПС + Символы.ПС;
			Если Итератор = КоличествоНовостей Тогда
				прервать;
			КонецЕсли;
			Итератор = Итератор + 1;
		КонецЦикла;
	Конецесли;
КонецПроцедуры

&НаКлиенте
Процедура ЯзыкПоискаПриИзменении(Элемент)
	Если ЭтотОбъект.ЯзыкПоиска = Элементы.ЯзыкПоиска.СписокВыбора[1].Значение Тогда
		СтрокаПоиска = "Business";
	ИначеЕсли ЭтотОбъект.ЯзыкПоиска = Элементы.ЯзыкПоиска.СписокВыбора[0].Значение Тогда
		СтрокаПоиска = "Бизнес";
	КонецЕсли;
КонецПроцедуры

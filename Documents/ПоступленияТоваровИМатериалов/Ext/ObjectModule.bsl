Процедура ОбработкаПроведения(Отказ, Режим)
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.УчетнаяПолитика = Перечисления.ВидыУчетнойПолитики.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли;
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступленияТоваровИМатериаловТовары.Номенклатура КАК Номенклатура,
	               |	СУММА(ПоступленияТоваровИМатериаловТовары.Количество) КАК Количество,
	               |	СУММА(ПоступленияТоваровИМатериаловТовары.Сумма) КАК Сумма,
	               |	ПоступленияТоваровИМатериаловТовары.СрокГодности КАК СрокГодности,
	               |	ПоступленияТоваровИМатериаловТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	               |ИЗ
	               |	Документ.ПоступленияТоваровИМатериалов.Товары КАК ПоступленияТоваровИМатериаловТовары
	               |ГДЕ
	               |	ПоступленияТоваровИМатериаловТовары.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоступленияТоваровИМатериаловТовары.Номенклатура,
	               |	ПоступленияТоваровИМатериаловТовары.СрокГодности";
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			Движение = Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Сумма = Выборка.Сумма;
			Движение.Склад = Склад;
			Движение.Количество = Выборка.Количество;
			Если ОтражатьСрокиГодности Тогда
				Движение.СрокГодности = Выборка.СрокГодности;
			КонецЕсли;
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
			Движение.Период = Дата;
			Движение.Сумма = Выборка.Сумма;
			Движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Поставщик;
		КонецЦикла;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если Не ЗначениеЗаполнено(АвторДокумента) Тогда
		АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	СуммаДокумента = Товары.Итог("Сумма");
КонецПроцедуры
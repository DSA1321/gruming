&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.АвторДокумента) Тогда
		Объект.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ПризнакОплаты = Перечисления.ОплатаДокумента.НеОплачен;
	Иначе
		Если Объект.ПризнакОплаты <> Перечисления.ОплатаДокумента.ПолностьюОплачен Тогда
			СтруктураОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Объект.Ссылка);
			Объект.ПризнакОплаты = СтруктураОплаты.ПризнакОплаты;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("Основание") И ТипЗНЧ(Параметры.Основание) = Тип("ДокументСсылка.ЗаписьКлиента") Тогда		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РеализацияТоваровИУслуг.Ссылка КАК Ссылка,
		|	РеализацияТоваровИУслуг.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	Документ.РеализацияТоваровИУслуг КАК РеализацияТоваровИУслуг
		|ГДЕ
		|	РеализацияТоваровИУслуг.ДокументОснование = &ДокументОснование
		|	И НЕ РеализацияТоваровИУслуг.ПометкаУдаления";
		Запрос.УстановитьПараметр("ДокументОснование", Параметры.Основание);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СообщениеПользователю = Новый СообщениеПользователю();
			СообщениеПользователю.Текст = СтрШаблон("Для документа уже введен документ реализации ""%1"" !", Выборка.Ссылка);
			СообщениеПользователю.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Услуги.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
		СтрокаТЧ.Стоимость = РаботаСЦенами.ПолучитьЦенуПродажиНаДату(СтрокаТЧ.Номенклатура,, Объект.Дата);
	Иначе
		СтрокаТЧ.Стоимость = 0;
	КонецЕсли;
	ПолучитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтоимостьПриИзменении(Элемент)
	ПолучитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Товары.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
		СтрокаТЧ.Цена = РаботаСЦенами.ПолучитьЦенуПродажиНаДату(СтрокаТЧ.Номенклатура,, Объект.Дата);
	Иначе
		СтрокаТЧ.Цена = 0;
	КонецЕсли;
	РаботаСТабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
	ПолучитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Товары.ТекущиеДанные;
	РаботаСТабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
	ПолучитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	СтрокаТЧ = Элементы.Товары.ТекущиеДанные;
	РаботаСТабличнымиЧастямиКлиент.ПересчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТЧ);
	ПолучитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСуммуДокумента()
	Объект.СуммаДокумента = Объект.Услуги.Итог("Стоимость") + Объект.Товары.Итог("Сумма");
КонецПроцедуры